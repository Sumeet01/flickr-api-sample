apply plugin: "com.android.application"
apply plugin: "com.jakewharton.butterknife"

def supportVersion = "25.1.1"
def okhttpVersion = "3.6.0"
def butterknifeVersion = "8.5.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.gturedi.flickr"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode getVersionCode()
        versionName "1.0.$versionCode"
        vectorDrawables.useSupportLibrary = true
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    compile "com.google.code.gson:gson:2.6.1"
    compile "com.jakewharton.timber:timber:4.5.1"
    compile "org.greenrobot:eventbus:3.0.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'de.hdodenhof:circleimageview:2.0.0'
}

def getVersionCode() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    return versionProps['VERSION_CODE'].toInteger()
}

task increaseVersionCodeTask << {
    println "increaseVersionCode task started.."
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    def code = versionProps['VERSION_CODE'].toInteger() + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug') {
        task.dependsOn increaseVersionCodeTask
    }
}