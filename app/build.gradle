apply plugin: "com.android.application"
apply plugin: "com.jakewharton.butterknife"

def SUPPORT_VERSION = "25.1.1"
def OKHTTP_VERSION = "3.6.0"
def BUTTERKNIFE_VERSION = "8.5.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.gturedi.flickr"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode readVersionCode()
        versionName "1.0.$versionCode"
        vectorDrawables.useSupportLibrary = true
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"
    compile "com.google.code.gson:gson:2.7"
    compile "com.jakewharton.timber:timber:4.5.1"
    compile "org.greenrobot:eventbus:3.0.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'de.hdodenhof:circleimageview:2.0.0'
}

def readVersionCode() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    return versionProps["VERSION_CODE"].toInteger()
}

task increaseVersionCodeTask << {
    println "increaseVersionCode task started.."
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    def code = versionProps["VERSION_CODE"].toInteger() + 1
    versionProps["VERSION_CODE"] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug') {
        task.dependsOn increaseVersionCodeTask
    }
}